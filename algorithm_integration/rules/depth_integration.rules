"""
depth_integration.rules
Matthew Stone

Merge depth caller CNV across programs and samples.
"""

CNV = config['cnv_types']

def variant_prefix(wildcards):
    return '{batch}_depth_{svtype}_{chrom}'.format(**wildcards)

# Cluster CNV calls
rule bedcluster:
    input:
        bed='depth/input_beds/{batch}.{svtype}.bed.gz'
    output:
        bed='depth/bedcluster/{batch}.{svtype}.{chrom}.bed'
    params:
        prefix=variant_prefix,
        **config['bedcluster']
    shell:
        """
        svtools bedcluster {input.bed} -r {wildcards.chrom} \
            -p {params.prefix} -f {params.frac} --merge-coordinates > {output}
        """

# Aggregate observations into variants and convert to RdTest format
rule make_depth_rdtest_beds:
    input:
        expand(rules.bedcluster.output.bed, svtype=CNV, 
               batch='{batch}', chrom='{chrom}')
    output:
        'depth/rdtest_beds/{batch}.depth.{chrom}.bed'
    shell:
        """
        cat \
            <(./scripts/make_depth_rdtest_bed.py {input[0]} | sed '1d') \
            <(./scripts/make_depth_rdtest_bed.py {input[1]} | sed '1d') \
          | sort -k1,1V -k2,2n \
          | cat <(echo -e "#chrom start end name samples svtype" | sed -e 's/ /\\t/g') - \
          > {output}
        """
