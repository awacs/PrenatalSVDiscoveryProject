
configfile: 'config.yaml'

SOURCES = config['pesr_sources']
SVTYPES = config['svtypes']

with open(config['quads']) as qlist:
    QUADS = [q.strip() for q in qlist.readlines()]

wildcard_constraints:
    source='(' + '|'.join(SOURCES) + ')'

rule all:
    input:
        expand('filtered_vcfs/{source}.{quad}.vcf',
               source=SOURCES, quad=QUADS)
    output:
        touch('all.done')

rule standardize_vcf:
    input:
        'raw_vcfs/{source}/{source}.{quad}.vcf.gz'
    output:
        'std_vcfs/{source}.{quad}.vcf'
    shell:
        """
        standardize-vcf {input} {output} {wildcards.source}
        """

rule identify_vcf_outliers:
    input:
        expand('std_vcfs/{{source}}.{quad}.vcf', quad=QUADS)
    output:
        'outliers/{source}.list'
    params:
        svtypes=SVTYPES
    script:
        "scripts/find_outliers.py"

rule remove_vcf_outliers:
    input:
        vcf='std_vcfs/{source}.{quad}.vcf',
        outliers='outliers/{source}.list'
    output:
        'filtered_vcfs/{source}.{quad}.vcf'
    script:
        "scripts/remove_outliers.py"

