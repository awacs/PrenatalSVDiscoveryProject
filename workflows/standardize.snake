
configfile: 'config/config.yaml'

SOURCES = config['pesr_sources']
SVTYPES = 'DEL DUP INV BND'.split()

with open(config['quads']) as qlist:
    QUADS = [q.strip() for q in qlist.readlines()]

wildcard_constraints:
    source='(' + '|'.join(SOURCES) + ')'

rule all:
    input:
        expand('data/filtered_vcfs/{source}.{quad}.vcf',
               source=SOURCES, quad=QUADS)
#         expand('data/outliers/{source}.list', 
#                source=SOURCES)
#        expand('std_vcfs/{source}.{quad}.vcf', 
#               source=SOURCES, quad=QUADS)

rule standardize_vcf:
    input:
        'data/raw_vcfs/{source}/{source}.{quad}.vcf.gz'
    output:
        'data/std_vcfs/{source}.{quad}.vcf'
    shell:
        """
        standardize-vcf {input} {output} {wildcards.source}
        """

rule identify_vcf_outliers:
    input:
        expand('data/std_vcfs/{{source}}.{quad}.vcf', quad=QUADS)
    output:
        'data/outliers/{source}.list'
    params:
        svtypes=SVTYPES
    script:
        "scripts/find_outliers.py"

rule remove_vcf_outliers:
    input:
        vcf='data/std_vcfs/{source}.{quad}.vcf',
        outliers='data/outliers/{source}.list'
    output:
        'data/filtered_vcfs/{source}.{quad}.vcf'
    script:
        "scripts/remove_outliers.py"

