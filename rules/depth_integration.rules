"""
rd_merged.snake
Matthew Stone


Merge depth caller CNV across programs and samples.

Input beds are product of cnv_clean; columns=chr,start,end,svtype,names

1) bedtools merge to collapse CNV across programs within each sample
2) 50% reciprocal overlap across samples
3) bedcluster
"""

SOURCES = config['depth_sources']
CNV = config['cnv_types']

with open(config['chroms']) as clist:
    CHROMS = [c.strip() for c in clist.readlines()]

# Intersect merged calls across samples
rule depth_intersect:
    input:
        bed='preprocessing/std_beds/cohort.{svtype}.bed.gz'
    output:
        bed='integration/depth/intersection/cohort.{svtype}.{chrom}.bed.gz'
    params:
        frac=0.8
    shell:
        """
        bedtools intersect \
          -a <(tabix {input} {wildcards.chrom} | cut -f -6) \
          -b <(tabix {input} {wildcards.chrom} | cut -f -6) \
          -wa -wb -loj -r -f {params.frac} | bgzip -c > {output}
        """

# List of unique variant lists necessary for running bedcluster
rule make_depth_variant_lists:
    input:
        bed='preprocessing/std_beds/cohort.{svtype}.bed.gz'
    output:
        list='integration/depth/variant_lists/cohort.{svtype}.{chrom}.list'
    shell:
        """
        tabix {input} {wildcards.chrom} | cut -f4 > {output}
        """

def variant_prefix(wildcards):
    return 'depth_{0}_{1}'.format(wildcards.svtype, wildcards.chrom)

# Cluster intersected bed
rule bedcluster:
    input:
        intersect_bed=rules.depth_intersect.output.bed,
        variant_list=rules.make_depth_variant_lists.output.list
    output:
        bed='integration/depth/bedcluster/cohort_split.{svtype}.{chrom}.bed'
    params:
        prefix=variant_prefix
    shell:
        """
        zcat {input.intersect_bed} \
          | svtools bedcluster {input.variant_list} -p {params.prefix} --merge-coordinates \
          > {output}
        """

# Merge split bed
rule merge_across_svtypes:
    input:
        expand(rules.bedcluster.output.bed, svtype=CNV, chrom='{chrom}')
    output:
        bed='integration/depth/bedcluster/cohort.{chrom}.bed'
    shell:
        """
        head -n1 {input[0]} > {output};
        for f in {input}; do sed '1d' $f | sort -k1,1V -k2,2n >> {output}; done
        """

rule make_depth_rdtest_bed:
    input:
        bed=rules.merge_across_svtypes.output.bed
    output:
        'integration/depth/rdtest_beds/cohort.{chrom}.bed'
    script:
        "scripts/make_depth_rdtest_beds.py"

rule rdtest_filter_depth:
    input:
        bed=rules.merge_across_svtypes.output.bed,
        list='rdtest/depth/cohort.rdtest_pass.{chrom}.list'
    output:
        bed='integration/depth/rdtest_filtered/cohort.{chrom}.bed'
    shell:
        """
        fgrep -w -f {input.list} {input.bed} \
          | cat <(head -n1 {input.bed}) - \
          > {output}
        """
