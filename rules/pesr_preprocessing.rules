

SOURCES = config['pesr_sources']
SVTYPES = config['svtypes']

with open(config['quads']) as qlist:
    QUADS = [q.strip() for q in qlist.readlines()]

rule standardize_vcf:
    input:
        'data/raw_vcfs/{source}/{source}.{quad}.vcf.gz'
    output:
        vcf='preprocessing/std_vcfs/{source}.{quad}.vcf'
    shell:
        """
        standardize_vcf {input} {output} {wildcards.source}
        """

rule identify_vcf_outliers:
    input:
        expand(rules.standardize_vcf.output.vcf, quad=QUADS, source='{source}')
    output:
        outliers='preprocessing/outliers/{source}.list'
    params:
        svtypes=SVTYPES
    script:
        "scripts/find_outliers.py"

rule remove_vcf_outliers:
    input:
        vcf=rules.standardize_vcf.output.vcf,
        outliers=rules.identify_vcf_outliers.output.outliers
    output:
        vcf=temp('preprocessing/filtered_vcfs/{source}.{quad}.vcf')
    script:
        "scripts/remove_outliers.py"

rule sort_vcfs:
    input:
        vcf=rules.remove_vcf_outliers.output.vcf
    output:
        vcf='preprocessing/filtered_vcfs/{source}.{quad}.vcf.gz',
        idx='preprocessing/filtered_vcfs/{source}.{quad}.vcf.gz.tbi',
    shell:
        """
        vcf-sort -c {input} | bgzip -c > {output.vcf};
        tabix -p vcf {output.vcf}
        """

